import java.util.concurrent.TimeUnit

plugins {
	id("application")
	id("java")
	id("wrapper")
	
	id("com.gradleup.shadow").version("8.3.8")
}

version = "1.5.1-SNAPSHOT"

final isSnapshot = project.version.endsWith("-SNAPSHOT")

final SOURCE_ENCODING = "UTF-8"

final snapshotsRepoUrl = "https://central.sonatype.com/repository/maven-snapshots/"
repositories {
	mavenCentral()
	if (isSnapshot) {
		maven {
			name = "Sonatype snapshots"
			url = snapshotsRepoUrl
		}
	}
}

configurations.all {
	resolutionStrategy {
		cacheChangingModulesFor(0, TimeUnit.SECONDS)
	}
}

dependencies {
	implementation("ch.qos.logback:logback-classic:1.3.15")
	implementation("com.google.guava:guava:33.5.0-jre")
	implementation(platform("de.hhu.stups:prob-java-bom:4.15.1-SNAPSHOT"))
	implementation("de.hhu.stups:de.prob2.kernel")
	implementation("io.github.spencerpark:jupyter-jvm-basekernel:2.3.0")
	implementation("org.jetbrains:annotations:26.0.2-1")
	implementation("se.sawano.java:alphanumeric-comparator:1.4.1")
}

java {
	sourceCompatibility = JavaVersion.VERSION_1_8
}

tasks.withType(JavaCompile) {
	options.encoding = SOURCE_ENCODING
}

final firstLineOfProcessOutput = {ExecOutput execOutput ->
	return execOutput.standardOutput.asText.map {it.split('\n')[0]}
}

processResources {
	inputs.property("version", project.version)
	inputs.property("currentGitCommit", firstLineOfProcessOutput(providers.exec {
		executable = "git"
		args = ["rev-parse", "HEAD"]
	}))

	filesMatching("de/prob2/jupyter/build.properties") {
		expand(version: inputs.properties["version"], commit: inputs.properties["currentGitCommit"])
	}
}

application {
	mainClass = "de.prob2.jupyter.Main"
}

task installKernelSpec(type: Exec) {
	dependsOn = [shadowJar]
	executable = project.hasProperty("pythonCommand") ? project.pythonCommand : "python3"
	args = [shadowJar.archiveFile.get().asFile, "--jar-path=" + shadowJar.archiveFile.get().asFile, "install"]
	if (project.hasProperty("kernelspecUserInstall") && project.kernelspecUserInstall == "true") {
		args("--user")
	}
}
